#!/usr/bin/env perl
use strict;
use warnings;
use feature 'say';

my @index = `git diff --color --stat --cached`;
if (@index) {
    say "\e[0;33mIndex:\e[m";
    print @index;
}

my @wc = `git diff --color --stat`;

if (@wc) {
    say "\e[1;31mWorking tree:\e[m";
    s/(\S+)/\e[1;37m$1\e[m/ for @wc[0 .. $#wc-1];
    print @wc;
}

my $untracked_files = `git ls-files -z --exclude-standard --others --full-name`;
if ($untracked_files) {
    say "\e[1;30mUntracked files:\e[m";
    my @untracked = map { " $_\n" } split "\0", $untracked_files;
    s/(\S+)/\e[1;37m$1\e[m/ for @untracked;
    print @untracked;
}

my @branches = map { s/^\s+//; s/\s+$//; s/^\*\s*//; $_ } split /\n/, `git branch -l`;
my %branch_output;
for my $branch (@branches) {
    my $output = `git rev-list $branch\@{u}..$branch 2>&1`;
    if ($output =~ /No upstream branch found/ || $output =~ /unknown revision/) {
        $branch_output{$branch} = "No upstream";
        next;
    }

    my $status;
    if (my $lines = $output =~ tr/\n/\n/) {
        $status .= "+$lines";
    }

    my $reverse = `git rev-list $branch..$branch\@{u} 2>&1`;
    if (my $reverse_lines = $reverse =~ tr/\n/\n/) {
        $status .= "-$reverse_lines";
    }

    $branch_output{$branch} = $status
        if $status;
}

if (%branch_output) {
    say "\e[0;33mLocal branches:\e[m";
    for my $branch (sort keys %branch_output) {
        say " $branch: $branch_output{$branch}"
    }
}
